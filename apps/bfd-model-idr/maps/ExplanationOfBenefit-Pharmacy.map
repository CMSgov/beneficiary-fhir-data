//See this link for lexer order -> https://github.com/hapifhir/org.hl7.fhir.core/blob/c06051838f9bcd5fc0b552df960f5bf2fcf2dbb4/org.hl7.fhir.r4/src/main/java/org/hl7/fhir/r4/utils/StructureMapUtilities.java#L757
//Must appear in order map -> conceptmap -> uses -> imports -> groups

map "https://bfd.cms.gov/MappingLanguage/Maps/ExplanationOfBenefit-Pharmacy" = "BFD-ExplanationOfBenefit-Pharmacy-Map"

uses "https://bfd.cms.gov/fhir/StructureDefinition/ExplanationOfBenefit-Pharmacy" alias ExplanationOfBenefitPharmacy as source
uses "http://hl7.org/fhir/StructureDefinition/LineItemComponent" alias LineItemComponent as source
uses "http://hl7.org/fhir/StructureDefinition/SupportingInfoComponent" alias SupportingInfoComponent as source

//The first specified target is what the default output will output as the ResourceType
uses "http://hl7.org/fhir/StructureDefinition/ExplanationOfBenefit" alias BFDEOB as target

imports "https://bfd.cms.gov/MappingLanguage/Maps/ExplanationOfBenefit-Helper" 
imports "https://bfd.cms.gov/MappingLanguage/Maps/ExplanationOfBenefit-Base" 

group createEOBPharmacy(source src: ExplanationOfBenefitPharmacy, target tgt: BFDEOB){
    
    //Even though we may have "cancelled" claims, status refers to the resource itself.
    src -> tgt.status = "active" "Set status"; 
    src -> tgt.use = "claim" "Set use code";
    src -> tgt.outcome = "complete" "Set outcome";
    
    src -> tgt.type = create('CodeableConcept') as tgtCC then {
      src.CLM_TYPE_CD as typeCd -> tgtCC.coding = translate(typeCd,'https://bfd.cms.gov/MappingLanguage/Maps/ExplanationOfBenefit-Helper#CLM_TYPE_CD','coding') "translate code";
      src.CLM_TYPE_CD as typeCd -> tgtCC.coding = c('https://bluebutton.cms.gov/fhir/CodeSystem/CLM-TYPE-CD', typeCd) "Add claim type code";
    } "Create type";

    src -> tgt.patient = create('Reference') as tgtReference then {
      src.CLM_BENE_XREF_EFCTV_SK as ptId -> tgtReference.reference = append("Patient/",ptId) "Concatenate patient id";
    } "Add patient reference to EOB";

    src -> tgt.meta = create('Meta') as tgtMeta then {
      src.lastUpdated as lastUpdated -> tgtMeta.lastUpdated = lastUpdated "set lastUpdated";
      src.CLM_TYPE_CD as typeCd -> tgtMeta.profile = translate(typeCd,'https://bfd.cms.gov/MappingLanguage/Maps/ExplanationOfBenefit-Helper#profile_metadata','code') "set profile";
      src.CLM_SRC_ID as sourceSk -> tgtMeta.source = translate(sourceSk, 'https://bfd.cms.gov/MappingLanguage/Maps/ExplanationOfBenefit-Helper#CLM_SRC_ID','code') "set source";
      src.CLM_SRC_ID as sourceSk -> tgtMeta.tag = translate(sourceSk, 'https://bfd.cms.gov/MappingLanguage/Maps/ExplanationOfBenefit-Helper#ADJUDICATION-STATUS','coding') "set source";
    } "Create metadata";

    src -> tgt.identifier = create('Identifier') as tgtIdentifier then {
          src -> tgtIdentifier.type = cc("http://hl7.org/fhir/us/carin-bb/CodeSystem/C4BBIdentifierType", "uc") "Add identifier type";
          src.CLM_UNIQ_ID as claimId -> tgtIdentifier.value = claimId "Set identifier Value";
    } "Add CLM_UNIQ_ID as unique identifier";

    src -> tgt.identifier = create('Identifier') as tgtIdentifier then {
      src -> tgtIdentifier.system = "https://bluebutton.cms.gov/identifiers/CLM-CNTL-NUM" "set system";
      src.CLM_ORIG_CNTL_NUM as claimCntlNum -> tgtIdentifier.value = claimCntlNum "Set identifier Value";
    } "Add CLM_ORIG_CNTL_NUM as identifier";

    src -> tgt.billablePeriod = create('Period') as billablePeriod then {
      src.CLM_FROM_DT as fromDate -> billablePeriod.start = fromDate "Set from date";
      src.CLM_THRU_DT as toDate -> billablePeriod.end = toDate "Set to date";
    } "Set EOB period";

    src.CLM_IDR_LD_DT as idrLdDt -> tgt.extension = create('Extension') as tgtExtension then{
      src -> tgtExtension.url = "https://bluebutton.cms.gov/fhir/StructureDefinition/CLM-IDR-LD-DT" "Add url";
      src -> tgtExtension.value = idrLdDt "add date";
    } "add idr load date";
    
    src.CLM_EFCTV_DT as efctvDate -> tgt.created = efctvDate "Add created date for EOB";

    // Insurer for Part D claims
    src.CLM_TYPE_CD as typeCd where (typeCd >= 1 and typeCd <= 4) -> tgt.contained = create('Organization') as tgtOrg then createInsurerOrgPartD(src,tgtOrg) "Create insurer org.";
    src.CLM_TYPE_CD as typeCd where (typeCd >= 1 and typeCd <= 4) -> tgt.insurer = create('Reference') as tgtOrgRef, 
    tgtOrgRef.reference = "#insurer-org" "Set reference to contained insurer org";

    // Payment date
    src.CLM_PD_DT as paidDate -> tgt.payment = create('BackboneElement') as tgtBBElement then {
      src -> tgtBBElement.date = paidDate "Add paid date";
    } "CLM_PD_DT";

    // CMS process date - from CLM_DT_SGNTR
    src.CLM_CMS_PROC_DT as procDt -> tgt.extension = create('Extension') as tgtExtension then {
      src -> tgtExtension.url = "https://bluebutton.cms.gov/fhir/StructureDefinition/CLM-CMS-PROC-DT" "Add url";
      procDt -> tgtExtension.value = procDt "set process date";
    } "CLM_CMS_PROC_DT";

    // Provider organization for pharmacy - only create when needed based on NPI_TYPE

    src.PRVDR_SRVC_PRVDR_NPI_NUM as blgNPI -> tgt then{
      //We don't populate careTeamType when it's the provider, so we can uniquely identify it as billing.
        src -> tgt.provider = create('Reference') as tgtRef, tgtRef.reference = append("#", blgNPI) "Add reference to NPI";
        src.providerList as provider where provider.isDuplicate.exists().not() and provider.careTeamType.exists().not() and provider.NPI_TYPE = 1 -> tgt.contained = create('Practitioner') as tgtPract then createPractitioner(provider, tgtPract) "Add Practitioner";
        src.providerList as provider where provider.isDuplicate.exists().not() and provider.careTeamType.exists().not() and provider.NPI_TYPE = 2 -> tgt.contained = create('Organization') as tgtOrg then createOrg(provider, tgtOrg) "Add Organization";
    } "Add billing provider";

    src.providerList as pList where pList.careTeamType.exists() -> tgt then {
      pList where pList.NPI_TYPE = 1 and pList.isDuplicate.exists().not() -> tgt.contained = create('Practitioner') as tgtPract then createPractitioner(pList, tgtPract) "Add Practitioner";
      pList where pList.NPI_TYPE = 2 and pList.isDuplicate.exists().not() -> tgt.contained = create('Organization') as tgtOrg then createOrg(pList, tgtOrg) "Add Organization";

      src -> tgt.careTeam = create('BackboneElement') as tgtBBElement then {
        pList.careTeamType as careTeamType -> tgtBBElement.role = cc("http://hl7.org/fhir/us/carin-bb/CodeSystem/C4BBClaimCareTeamRole", careTeamType) "Add role";
        pList.careTeamSequenceNumber as seqNum -> tgtBBElement.sequence = seqNum "Set sequence";
        src -> tgtBBElement.provider = create('Reference') as tgtReference then {
          pList.PRVDR_SK as npi -> tgtReference.reference = append("#", npi) "Add reference";
        } "Add reference to EOB";
        
        pList.taxonomyCodes as taxonomyCodes -> tgtBBElement.qualification = create('CodeableConcept') as qualCC then{
          taxonomyCodes -> qualCC.coding = c("http://nucc.org/provider-taxonomy", taxonomyCodes) "add taxonomy";
        } "Add taxonomy/qualifications";
      } "Add CareTeam Element";

    } "Add CareTeam providers";

    src.supportingInfoComponents as infoComponent -> tgt.supportingInfo = create('BackboneElement') as tgtSupportingInfo then addPharmacySupportingInfo(infoComponent,tgtSupportingInfo) "add pharmacy supportingInfo";

    src.lineItemComponents as itemComponent -> tgt.item = create('BackboneElement') as tgtItem then addPharmacyItem(itemComponent,tgtItem) "add pharmacy items";

    // Benefit payment status based on pricing exception code from line items
    //Note, blank = in network! Null -> treat as blank (in network)
    src.lineItemComponents as lineItem -> tgt.adjudication = create('BackboneElement') as tgtAdj then {
      src -> tgtAdj.category = cc("http://hl7.org/fhir/us/carin-bb/CodeSystem/C4BBAdjudicationDiscriminator", "benefitpaymentstatus") "Set category";
      lineItem.CLM_PRCNG_EXCPTN_CD as pricingCode -> tgtAdj.reason = create('CodeableConcept') as tgtCC then {
        pricingCode where pricingCode = "" -> tgtCC.coding = c("http://hl7.org/fhir/us/carin-bb/CodeSystem/C4BBPayerAdjudicationStatus", "innetwork") "Set in-network";
        pricingCode where pricingCode = "O" -> tgtCC.coding = c("http://hl7.org/fhir/us/carin-bb/CodeSystem/C4BBPayerAdjudicationStatus", "outofnetwork") "Set out-of-network";
        pricingCode where pricingCode = "M" -> tgtCC.coding = c("http://hl7.org/fhir/us/carin-bb/CodeSystem/C4BBPayerAdjudicationStatus", "other") "Set other";
      } "Set reason";
    } "Add benefit payment status";
    
    // Insurance coverage
    src.CLM_TYPE_CD as claimType -> tgt.insurance = create('BackboneElement') as tgtBBElement then {
      src -> tgtBBElement.focal = true "Add focal";
      src -> tgtBBElement.coverage = create('Reference') as tgtReference then {
        claimType -> tgtReference.display = "Part D" "Add coverage type";
      } "Add reference for coverage";
    } "Add insurance coverage";

    src.CLM_BENE_PMT_AMT as benePmtAmt -> tgt.total = create('BackboneElement') as tgtBBElement then {  
      src -> tgtBBElement.category = create('CodeableConcept') as tgtCC then {
        src -> tgtCC.coding = c("http://hl7.org/fhir/us/carin-bb/CodeSystem/C4BBAdjudication", "paidbypatient") "Add first coding";
        src -> tgtCC.coding = c("https://bluebutton.cms.gov/fhir/CodeSystem/Adjudication", "CLM_BENE_PMT_AMT") "Add second coding";
     } "Add codeable concept";
     src -> tgtBBElement.amount = create('Money') as tgtMoney then addMoney(benePmtAmt, tgtMoney) "Add Money";
    } "Add bene payment amount";

    src.CLM_OTHR_TP_PD_AMT as othrTpPdAmt -> tgt.total = create('BackboneElement') as tgtBBElement then {
     src -> tgtBBElement.category = cc("https://bluebutton.cms.gov/fhir/CodeSystem/Adjudication", "CLM_OTHR_TP_PD_AMT") "Add coding 2";
     src -> tgtBBElement.amount = create('Money') as tgtMoney then addMoney(othrTpPdAmt, tgtMoney) "Add Money";
    } "Add other third party paid amount";

    src.CLM_ADJSTMT_TYPE_CD as adjTypeCd -> tgt.extension = create('Extension') as tgtExtension then {
      src -> tgtExtension.url = "https://bluebutton.cms.gov/fhir/StructureDefinition/CLM-ADJSTMT-TYPE-CD" "Add url";
      src -> tgtExtension.value = create('Coding') as tgtCoding then{
        src -> tgtCoding.system = "https://bluebutton.cms.gov/fhir/CodeSystem/CLM-ADJSTMT-TYPE-CD",
        tgtCoding.code = adjTypeCd "Add code";
      } "Add coding";
    } "CLM_ADJSTMT_TYPE_CD";


}

group addMoney(source src: String, target tgt: Money){
  src -> tgt.currency = "USD" "Set Currency";
  src as amt -> tgt.value = cast(amt, "decimal") "Set amount";
}
group addPharmacySupportingInfo(source src: SupportingInfoComponent, target tgt: BackboneElement){
    src.ROW_NUM as seqNum -> tgt.sequence = seqNum "set sequence number";
    
    // Refills authorized (always zero in IDR data)
    src.CLM_LINE_AUTHRZD_FILL_NUM as refills -> tgt then addRefillsAuthorized(src, tgt) "Add refills authorized";
    
    // Pharmacy service type
    src.CLM_PHRMCY_SRVC_TYPE_CD as pharmType -> tgt then addPharmacyServiceType(src, tgt) "Add pharmacy service type";
    
    // Prescription origin code
    src.CLM_LINE_RX_ORGN_CD as rxOrigin -> tgt then addPrescriptionOrigin(src, tgt) "Add prescription origin";
    
    // Brand generic indicator
    src.CLM_BRND_GNRC_CD as brandGeneric -> tgt then addBrandGenericIndicator(src, tgt) "Add brand generic indicator";
    
    // Patient residence code
    src.CLM_PTNT_RSDNC_CD as ptntRes -> tgt then addPatientResidence(src, tgt) "Add patient residence";
    
    // Submission clarification code
    src.CLM_LTC_DSPNSNG_MTHD_CD as submsnClr -> tgt then addSubmissionClarification(src, tgt) "Add submission clarification";
    
    // Compound code
    src.CLM_CMPND_CD as compound -> tgt then addCompoundCode(src, tgt) "Add compound code";
    
    // Days supply
    src.CLM_LINE_DAYS_SUPLY_QTY as daysSupply -> tgt then addDaysSupply(src, tgt) "Add days supply";
    
    // Fill number
    src.CLM_LINE_RX_FILL_NUM as fillNum -> tgt then addFillNumber(src, tgt) "Add fill number";
    
    // Dispense as written product selection
    src.CLM_DAW_PROD_SLCTN_CD as dawCode -> tgt then addDispenseAsWritten(src, tgt) "Add dispense as written";
    
    // Drug coverage status
    src.CLM_DRUG_CVRG_STUS_CD as drugCovStatus -> tgt then addDrugCoverageStatus(src, tgt) "Add drug coverage status";
    
    // Catastrophic coverage
    src.CLM_CTSTRPHC_CVRG_IND_CD as catCov -> tgt then addCatastrophicCoverage(src, tgt) "Add catastrophic coverage";

    src.CLM_LINE_RX_NUM as rxNumber -> tgt then addRxNum(src, tgt) "Add rx num";

    src.CLM_DSPNSNG_STUS_CD as dispCd -> tgt then addDispCd(src, tgt) "Add dispensing status code";

    // Claim submission format code
    src.CLM_SBMT_FRMT_CD as fmtCd -> tgt then addFmtCd(src, tgt) "Add claim submission format code";

    // Submitter contract number
    src.CLM_SBMTR_CNTRCT_NUM as sbmtrCntrctNum -> tgt then addSubmitterContractNumber(src, tgt) "Add submitter contract number";

    // Submitter contract PBP number
    src.CLM_SBMTR_CNTRCT_PBP_NUM as sbmtrCntrctPbpNum -> tgt then addSubmitterContractPbpNumber(src, tgt) "Add submitter contract pbp number";
}

group addRxNum(source src: SupportingInfoComponent, target tgt: BackboneElement){
  src -> tgt.category = create('CodeableConcept') as tgtCC then {
      src -> tgtCC.coding = c("http://terminology.hl7.org/CodeSystem/claiminformationcategory","info") "Add Coding";
      src -> tgtCC.coding = c("https://bluebutton.cms.gov/fhir/CodeSystem/Supporting-Information","CLM_LINE_RX_NUM") "Add Coding";
  } "Add category";
  src.CLM_LINE_RX_NUM as rxNum -> tgt.value = rxNum "Add RX Num";
}

group addPharmacyServiceType(source src: SupportingInfoComponent, target tgt: BackboneElement){
    src -> tgt.category = create('CodeableConcept') as tgtCC then {
        src -> tgtCC.coding = c("http://terminology.hl7.org/CodeSystem/claiminformationcategory","info") "Add Coding";
        src -> tgtCC.coding = c("https://bluebutton.cms.gov/fhir/CodeSystem/Supporting-Information","CLM_PHRMCY_SRVC_TYPE_CD") "Add Coding";
    } "Add category";
    src -> tgt.code = create('CodeableConcept') as tgtCC then{
        src.CLM_PHRMCY_SRVC_TYPE_CD as val -> tgtCC.coding = c("https://bluebutton.cms.gov/fhir/CodeSystem/CLM-PHRMCY-SRVC-TYPE-CD", val) "Add coding";
    } "Add CC";
}

group addPrescriptionOrigin(source src: SupportingInfoComponent, target tgt: BackboneElement){
    src -> tgt.category = create('CodeableConcept') as tgtCC then {
        src -> tgtCC.coding = translate("CLM_LINE_RX_ORGN_CD",'https://bfd.cms.gov/MappingLanguage/Maps/ExplanationOfBenefit-Helper#C4BB-SupportingInfo-Categories','coding') "set coding";
    } "set category";
    src -> tgt.code = create('CodeableConcept') as tgtCC then{
        src.CLM_LINE_RX_ORGN_CD as val -> tgtCC.coding = c("http://terminology.hl7.org/CodeSystem/NCPDPPrescriptionOriginCode", val) "Add coding";
        src.CLM_LINE_RX_ORGN_CD as val -> tgtCC.coding = c("https://bluebutton.cms.gov/fhir/CodeSystem/CLM-LINE-RX-ORGN-CD", val) "Add coding";
    } "Add CC";
}

group addBrandGenericIndicator(source src: SupportingInfoComponent, target tgt: BackboneElement){
    src -> tgt.category = create('CodeableConcept') as tgtCC then {
        src -> tgtCC.coding = translate("CLM_BRND_GNRC_CD",'https://bfd.cms.gov/MappingLanguage/Maps/ExplanationOfBenefit-Helper#C4BB-SupportingInfo-Categories','coding') "set coding";
    } "set category";
    src -> tgt.code = create('CodeableConcept') as tgtCC then{
        src.CLM_BRND_GNRC_CD as val -> tgtCC.coding = c("http://terminology.hl7.org/CodeSystem/NCPDPBrandGenericIndicator", val) "Add coding";
        src.CLM_BRND_GNRC_CD as val -> tgtCC.coding = c("https://bluebutton.cms.gov/fhir/CodeSystem/CLM-BRND-GNRC-CD", val) "Add coding";
    } "Add CC";
}

group addPatientResidence(source src: SupportingInfoComponent, target tgt: BackboneElement){
    src -> tgt.category = create('CodeableConcept') as tgtCC then {
        src -> tgtCC.coding = c("http://terminology.hl7.org/CodeSystem/claiminformationcategory","info") "Add Coding";
        src -> tgtCC.coding = c("https://bluebutton.cms.gov/fhir/CodeSystem/Supporting-Information","CLM_PTNT_RSDNC_CD") "Add Coding";
    } "Add category";
    src -> tgt.code = create('CodeableConcept') as tgtCC then{
        src.CLM_PTNT_RSDNC_CD as val -> tgtCC.coding = c("https://bluebutton.cms.gov/fhir/CodeSystem/CLM-PTNT-RSDNC-CD", val) "Add coding";
    } "Add CC";
}

group addSubmissionClarification(source src: SupportingInfoComponent, target tgt: BackboneElement){
    src -> tgt.category = create('CodeableConcept') as tgtCC then {
        src -> tgtCC.coding = c("http://terminology.hl7.org/CodeSystem/claiminformationcategory","info") "Add Coding";
        src -> tgtCC.coding = c("https://bluebutton.cms.gov/fhir/CodeSystem/Supporting-Information","CLM_LTC_DSPNSNG_MTHD_CD") "Add Coding";
    } "Add category";
    src -> tgt.code = create('CodeableConcept') as tgtCC then{
        src.CLM_LTC_DSPNSNG_MTHD_CD as val -> tgtCC.coding = c("https://bluebutton.cms.gov/fhir/CodeSystem/CLM-LTC-DSPNSNG-MTHD-CD", val) "Add coding";
    } "Add CC";
}

group addCompoundCode(source src: SupportingInfoComponent, target tgt: BackboneElement){
    src -> tgt.category = create('CodeableConcept') as tgtCC then {
        src -> tgtCC.coding = translate("CLM_CMPND_CD",'https://bfd.cms.gov/MappingLanguage/Maps/ExplanationOfBenefit-Helper#C4BB-SupportingInfo-Categories','coding') "set coding";
    } "set category";
    src -> tgt.code = create('CodeableConcept') as tgtCC then{
        src.CLM_CMPND_CD as val -> tgtCC.coding = c("http://terminology.hl7.org/CodeSystem/NCPDPCompoundCode", val) "Add coding";
        src.CLM_CMPND_CD as val -> tgtCC.coding = c("https://bluebutton.cms.gov/fhir/CodeSystem/CLM-CMPND-CD", val) "Add coding";
    } "Add CC";
}

group addDaysSupply(source src: SupportingInfoComponent, target tgt: BackboneElement){
    src -> tgt.category = create('CodeableConcept') as tgtCC then {
        src -> tgtCC.coding = translate("CLM_LINE_DAYS_SUPLY_QTY",'https://bfd.cms.gov/MappingLanguage/Maps/ExplanationOfBenefit-Helper#C4BB-SupportingInfo-Categories','coding') "set coding";
    } "set category";
    src.CLM_LINE_DAYS_SUPLY_QTY as daysSupply -> tgt.value = create('Quantity') as tgtQuantity then {
        daysSupply -> tgtQuantity.value = cast(daysSupply, 'integer') "Set days supply value";
        src -> tgtQuantity.unit = "days" "Set unit";
    } "Add days supply";
}

group addFillNumber(source src: SupportingInfoComponent, target tgt: BackboneElement){
    src -> tgt.category = create('CodeableConcept') as tgtCC then {
        src -> tgtCC.coding = translate("CLM_LINE_RX_FILL_NUM",'https://bfd.cms.gov/MappingLanguage/Maps/ExplanationOfBenefit-Helper#C4BB-SupportingInfo-Categories','coding') "set coding";
    } "set category";
    src.CLM_LINE_RX_FILL_NUM as fillNum -> tgt.value = create('Quantity') as tgtQuantity then {
        fillNum -> tgtQuantity.value = cast(fillNum, 'integer') "Set fill number value";
        src -> tgtQuantity.unit = "fill" "Set unit";
    } "Add fill number";
}

group addDispenseAsWritten(source src: SupportingInfoComponent, target tgt: BackboneElement){
    src -> tgt.category = create('CodeableConcept') as tgtCC then {
        src -> tgtCC.coding = translate("CLM_DAW_PROD_SLCTN_CD",'https://bfd.cms.gov/MappingLanguage/Maps/ExplanationOfBenefit-Helper#C4BB-SupportingInfo-Categories','coding') "set coding";
    } "set category";
    src -> tgt.code = create('CodeableConcept') as tgtCC then{
        src.CLM_DAW_PROD_SLCTN_CD as val -> tgtCC.coding = c("http://terminology.hl7.org/CodeSystem/NCPDPDispensedAsWrittenOrProductSelectionCode", val) "Add coding";
    } "Add CC";
}

group addDrugCoverageStatus(source src: SupportingInfoComponent, target tgt: BackboneElement){
    src -> tgt.category = create('CodeableConcept') as tgtCC then {
        src -> tgtCC.coding = c("http://terminology.hl7.org/CodeSystem/claiminformationcategory","info") "Add Coding";
        src -> tgtCC.coding = c("https://bluebutton.cms.gov/fhir/CodeSystem/Supporting-Information","CLM_DRUG_CVRG_STUS_CD") "Add Coding";
    } "Add category";
    src -> tgt.code = create('CodeableConcept') as tgtCC then{
        src.CLM_DRUG_CVRG_STUS_CD as val -> tgtCC.coding = c("https://bluebutton.cms.gov/fhir/CodeSystem/CLM-DRUG-CVRG-STUS-CD", val) "Add coding";
    } "Add CC";
}

group addCatastrophicCoverage(source src: SupportingInfoComponent, target tgt: BackboneElement){
    src -> tgt.category = create('CodeableConcept') as tgtCC then {
        src -> tgtCC.coding = c("http://terminology.hl7.org/CodeSystem/claiminformationcategory","info") "Add Coding";
        src -> tgtCC.coding = c("https://bluebutton.cms.gov/fhir/CodeSystem/Supporting-Information","CLM_CTSTRPHC_CVRG_IND_CD") "Add Coding";
    } "Add category";
    src -> tgt.code = create('CodeableConcept') as tgtCC then{
        src.CLM_CTSTRPHC_CVRG_IND_CD as val -> tgtCC.coding = c("https://bluebutton.cms.gov/fhir/CodeSystem/CLM-CTSTRPHC-CVRG-IND-CD", val) "Add coding";
    } "Add CC";
}

group addDispCd(source src: SupportingInfoComponent, target tgt: BackboneElement){
  src -> tgt.category = create('CodeableConcept') as tgtCC then {
      src -> tgtCC.coding = c("http://terminology.hl7.org/CodeSystem/claiminformationcategory","info") "Add Coding";
      src -> tgtCC.coding = c("https://bluebutton.cms.gov/fhir/CodeSystem/Supporting-Information","CLM_DSPNSNG_STUS_CD") "Add Coding";
  } "Add category";
  src -> tgt.code = create('CodeableConcept') as tgtCC then{
      src.CLM_DSPNSNG_STUS_CD as val -> tgtCC.coding = c("https://bluebutton.cms.gov/fhir/CodeSystem/CLM-DSPNSNG-STUS-CD", val) "Add coding";
  } "Add CC";
}

group addFmtCd(source src: SupportingInfoComponent, target tgt: BackboneElement){
  src -> tgt.category = create('CodeableConcept') as tgtCC then {
      src -> tgtCC.coding = c("http://terminology.hl7.org/CodeSystem/claiminformationcategory","info") "Add Coding";
      src -> tgtCC.coding = c("https://bluebutton.cms.gov/fhir/CodeSystem/Supporting-Information","CLM_SBMT_FRMT_CD") "Add Coding";
  } "Add category";
  src -> tgt.code = create('CodeableConcept') as tgtCC then{
      src.CLM_SBMT_FRMT_CD as val -> tgtCC.coding = c("https://bluebutton.cms.gov/fhir/CodeSystem/CLM-SBMT-FRMT-CD", val) "Add coding";
  } "Add CC";
}

group addSubmitterContractNumber(source src: SupportingInfoComponent, target tgt: BackboneElement){
  src -> tgt.category = create('CodeableConcept') as tgtCC then {
      src -> tgtCC.coding = c("http://terminology.hl7.org/CodeSystem/claiminformationcategory","info") "Add Coding";
      src -> tgtCC.coding = c("https://bluebutton.cms.gov/fhir/CodeSystem/Supporting-Information","CLM_SBMTR_CNTRCT_NUM") "Add Coding";
  } "Add category";
  src.CLM_SBMTR_CNTRCT_NUM as sbmtrCntrctNum -> tgt.value = sbmtrCntrctNum "Add submitter contract number";
}

group addSubmitterContractPbpNumber(source src: SupportingInfoComponent, target tgt: BackboneElement){
  src -> tgt.category = create('CodeableConcept') as tgtCC then {
      src -> tgtCC.coding = c("http://terminology.hl7.org/CodeSystem/claiminformationcategory","info") "Add Coding";
      src -> tgtCC.coding = c("https://bluebutton.cms.gov/fhir/CodeSystem/Supporting-Information","CLM_SBMTR_CNTRCT_PBP_NUM") "Add Coding";
  } "Add category";
  src.CLM_SBMTR_CNTRCT_PBP_NUM as sbmtrCntrctPbpNum -> tgt.value = sbmtrCntrctPbpNum "Add submitter contract PBP number";
}

group addRefillsAuthorized(source src: SupportingInfoComponent, target tgt: BackboneElement){
    src -> tgt.category = create('CodeableConcept') as tgtCC then {
        src -> tgtCC.coding = translate("CLM_LINE_AUTHRZD_FILL_NUM",'https://bfd.cms.gov/MappingLanguage/Maps/ExplanationOfBenefit-Helper#C4BB-SupportingInfo-Categories','coding') "set coding";
    } "set category";
    src.CLM_LINE_AUTHRZD_FILL_NUM as refills -> tgt.value = create('Quantity') as tgtQuantity then {
        refills -> tgtQuantity.value = cast(refills, 'integer') "Set refills authorized value";
        src -> tgtQuantity.unit = "refills" "Set unit";
    } "Add refills authorized";
}

group addPharmacyItem(source src: LineItemComponent, target tgt: BackboneElement){
    src.CLM_LINE_NUM as lineNum -> tgt.sequence = lineNum "set sequence number";
    
    src.CLM_LINE_FROM_DT as servicedDate -> tgt.serviced = servicedDate "Set serviced date";

    src.isCompound as compounded where compounded = "true" -> tgt.productOrService = cc("http://hl7.org/fhir/us/carin-bb/CodeSystem/C4BBCompoundLiteral","compound") "Add compounded item";
    src.isCompound as compounded where compounded = "true" -> tgt.detail = create('BackboneElement') as tgtDetail then {
        src -> tgtDetail.sequence = "1" "Add sequence number. Always 1.";
        src.CLM_LINE_NDC_CD as ndcCode -> tgtDetail.productOrService = cc("http://hl7.org/fhir/sid/ndc",ndcCode) "Add NDC coding";
        src.CLM_LINE_NDC_QTY as qty -> tgtDetail.quantity = create('Quantity') as tgtQuantity then {
            qty -> tgtQuantity.value = cast(qty, 'decimal') "Set quantity value";
            src.CLM_LINE_NDC_QTY_QLFYR_CD as qtyQual -> tgtQuantity.unit = qtyQual "Set quantity unit";
        } "Set quantity";
    } "Add detail";

    src.isCompound as compounded where compounded = "false" -> tgt then{
      src.CLM_LINE_NDC_CD as ndcCode -> tgt.productOrService = cc("http://hl7.org/fhir/sid/ndc",ndcCode) "Add NDC coding";
      src.CLM_LINE_NDC_QTY as qty -> tgt.quantity = create('Quantity') as tgtQuantity then {
            qty -> tgtQuantity.value = cast(qty, 'decimal') "Set quantity value";
            src.CLM_LINE_NDC_QTY_QLFYR_CD as qtyQual -> tgtQuantity.unit = qtyQual "Set quantity unit";
        } "Set quantity";
    } "Add non-compound item";

    src.CLM_LINE_CVRD_PD_AMT as coveredAmt -> tgt.adjudication = create('BackboneElement') as tgtAdj then {
      src -> tgtAdj.category = create('CodeableConcept') as tgtCC then {
        src -> tgtCC.coding = c("http://terminology.hl7.org/CodeSystem/adjudication", "benefit") "Add first coding";
        src -> tgtCC.coding = c("https://bluebutton.cms.gov/fhir/CodeSystem/Adjudication", "CLM_LINE_CVRD_PD_AMT") "Add second coding";
     } "Add codeable concept";
     src -> tgtAdj.amount = create('Money') as tgtMoney then addMoney(coveredAmt, tgtMoney) "Add Money";
    } "Add total covered amount";

    src.CLM_LINE_NCVRD_CHRG_AMT as noncoveredAmt -> tgt.adjudication = create('BackboneElement') as tgtAdj then {
      src -> tgtAdj.category = create('CodeableConcept') as tgtCC then {
        src -> tgtCC.coding = c("http://hl7.org/fhir/us/carin-bb/CodeSystem/C4BBAdjudication", "noncovered") "Add first coding";
        src -> tgtCC.coding = c("https://bluebutton.cms.gov/fhir/CodeSystem/Adjudication", "CLM_LINE_NCVRD_CHRG_AMT") "Add second coding";
     } "Add codeable concept";
     src -> tgtAdj.amount = create('Money') as tgtMoney then addMoney(noncoveredAmt, tgtMoney) "Add Money";
    } "Add total noncovered amount";

    src.CLM_LINE_BENE_PMT_AMT as benePmtAmt -> tgt.adjudication = create('BackboneElement') as tgtAdj then {  
      src -> tgtAdj.category = create('CodeableConcept') as tgtCC then {
        src -> tgtCC.coding = c("http://hl7.org/fhir/us/carin-bb/CodeSystem/C4BBAdjudication", "paidbypatient") "Add first coding";
        src -> tgtCC.coding = c("https://bluebutton.cms.gov/fhir/CodeSystem/Adjudication", "CLM_LINE_BENE_PMT_AMT") "Add second coding";
     } "Add codeable concept";
      benePmtAmt -> tgtAdj.amount = create('Money') as tgtMoney then addMoney(benePmtAmt, tgtMoney) "Add beneficiary payment amount";
    } "Add beneficiary payment amount";
    
    src.CLM_LINE_BENE_PD_AMT as beneAmt -> tgt.adjudication = create('BackboneElement') as tgtAdj then {
        src -> tgtAdj.category = create('CodeableConcept') as tgtCC then {
            src -> tgtCC.coding = c("http://hl7.org/fhir/us/carin-bb/CodeSystem/C4BBAdjudication", "paidtopatient") "Add first coding";
            src -> tgtCC.coding = c("https://bluebutton.cms.gov/fhir/CodeSystem/Adjudication", "CLM_LINE_BENE_PD_AMT") "Add second coding";
        } "Add codeable concept";
        beneAmt -> tgtAdj.amount = create('Money') as tgtMoney then addMoney(beneAmt, tgtMoney) "Add beneficiary amount";
    } "Add beneficiary paid amount";
    
    src.CLM_LINE_PRVDR_PMT_AMT as prvdrAmt -> tgt.adjudication = create('BackboneElement') as tgtAdj then {
        src -> tgtAdj.category = create('CodeableConcept') as tgtCC then {
            src -> tgtCC.coding = c("http://hl7.org/fhir/us/carin-bb/CodeSystem/C4BBAdjudication", "paidtoprovider") "Add first coding";
            src -> tgtCC.coding = c("https://bluebutton.cms.gov/fhir/CodeSystem/Adjudication", "CLM_LINE_PRVDR_PMT_AMT") "Add second coding";
        } "Add codeable concept";
        prvdrAmt -> tgtAdj.amount = create('Money') as tgtMoney then addMoney(prvdrAmt, tgtMoney) "Add provider amount";
    } "Add provider payment amount";

    src.CLM_LINE_GRS_ABOVE_THRSHLD_AMT as aboveThreshAmt -> tgt.adjudication = create('BackboneElement') as tgtAdj then {
        src -> tgtAdj.category = cc("https://bluebutton.cms.gov/fhir/CodeSystem/Adjudication", "CLM_LINE_GRS_ABOVE_THRSHLD_AMT") "Set category";
        aboveThreshAmt -> tgtAdj.amount = create('Money') as tgtMoney then addMoney(aboveThreshAmt, tgtMoney) "Add gross above threshold amount";
    } "Add gross drug cost above threshold amount";

    src.CLM_LINE_GRS_BLW_THRSHLD_AMT as belowThreshAmt -> tgt.adjudication = create('BackboneElement') as tgtAdj then {
        src -> tgtAdj.category = cc("https://bluebutton.cms.gov/fhir/CodeSystem/Adjudication", "CLM_LINE_GRS_BLW_THRSHLD_AMT") "Set category";
        belowThreshAmt -> tgtAdj.amount = create('Money') as tgtMoney then addMoney(belowThreshAmt, tgtMoney) "Add gross below threshold amount";
    } "Add gross drug cost below threshold amount";

    src.CLM_LINE_GRS_CVRD_CST_TOT_AMT as grsCvdCstTotAmt -> tgt.adjudication = create('BackboneElement') as tgtAdj then {
        src -> tgtAdj.category = cc("https://bluebutton.cms.gov/fhir/CodeSystem/Adjudication", "CLM_LINE_GRS_CVRD_CST_TOT_AMT") "Set category";
        grsCvdCstTotAmt -> tgtAdj.amount = create('Money') as tgtMoney then addMoney(grsCvdCstTotAmt, tgtMoney) "Add gross covered cost total amount";
    } "Add gross covered cost total amount";

    src.CLM_LINE_LIS_AMT as lisAmt -> tgt.adjudication = create('BackboneElement') as tgtAdj then {
        src -> tgtAdj.category = cc("https://bluebutton.cms.gov/fhir/CodeSystem/Adjudication", "CLM_LINE_LIS_AMT") "Set category";
        lisAmt -> tgtAdj.amount = create('Money') as tgtMoney then addMoney(lisAmt, tgtMoney) "Add LIS amount";
    } "Add Low Income Cost Sharing Subsidy Amount";

    src.CLM_LINE_TROOP_TOT_AMT as troopAmt -> tgt.adjudication = create('BackboneElement') as tgtAdj then {
        src -> tgtAdj.category = cc("https://bluebutton.cms.gov/fhir/CodeSystem/Adjudication", "CLM_LINE_TROOP_TOT_AMT") "Set category";
        troopAmt -> tgtAdj.amount = create('Money') as tgtMoney then addMoney(troopAmt, tgtMoney) "Add TROOP amount";
    } "Add Other True Out Of Pocket Paid Amount";

    src.CLM_LINE_PLRO_AMT as plroAmt -> tgt.adjudication = create('BackboneElement') as tgtAdj then {
        src -> tgtAdj.category = cc("https://bluebutton.cms.gov/fhir/CodeSystem/Adjudication", "CLM_LINE_PLRO_AMT") "Set category";
        plroAmt -> tgtAdj.amount = create('Money') as tgtMoney then addMoney(plroAmt, tgtMoney) "Add PLRO amount";
    } "Add Patient Liability Reduction Other Paid Amount";

    src.CLM_RPTD_MFTR_DSCNT_AMT as mftrDscntAmt -> tgt.adjudication = create('BackboneElement') as tgtAdj then {
        src -> tgtAdj.category = cc("https://bluebutton.cms.gov/fhir/CodeSystem/Adjudication", "CLM_RPTD_MFTR_DSCNT_AMT") "Set category";
        mftrDscntAmt -> tgtAdj.amount = create('Money') as tgtMoney then addMoney(mftrDscntAmt, tgtMoney) "Add manufacturer discount amount";
    } "Add Gap Discount Amount";

    src.CLM_LINE_INGRDNT_CST_AMT as ingrdntAmt -> tgt.adjudication = create('BackboneElement') as tgtAdj then {
        src -> tgtAdj.category = cc("https://bluebutton.cms.gov/fhir/CodeSystem/Adjudication", "CLM_LINE_INGRDNT_CST_AMT") "Set category";
        ingrdntAmt -> tgtAdj.amount = create('Money') as tgtMoney then addMoney(ingrdntAmt, tgtMoney) "Add ingredient cost amount";
    } "Add Ingredient Cost Amount";

    src.CLM_LINE_SRVC_CST_AMT as srvcAmt -> tgt.adjudication = create('BackboneElement') as tgtAdj then {
        src -> tgtAdj.category = cc("https://bluebutton.cms.gov/fhir/CodeSystem/Adjudication", "CLM_LINE_SRVC_CST_AMT") "Set category";
        srvcAmt -> tgtAdj.amount = create('Money') as tgtMoney then addMoney(srvcAmt, tgtMoney) "Add dispensing fee amount";
    } "Add Dispensing Fee";

    src.CLM_LINE_SLS_TAX_AMT as slsTaxAmt -> tgt.adjudication = create('BackboneElement') as tgtAdj then {
        src -> tgtAdj.category = cc("https://bluebutton.cms.gov/fhir/CodeSystem/Adjudication", "CLM_LINE_SLS_TAX_AMT") "Set category";
        slsTaxAmt -> tgtAdj.amount = create('Money') as tgtMoney then addMoney(slsTaxAmt, tgtMoney) "Add sales tax amount";
    } "Add Sales Tax Amount";

    src.CLM_LINE_VCCN_ADMIN_FEE_AMT as vccnFeeAmt -> tgt.adjudication = create('BackboneElement') as tgtAdj then {
        src -> tgtAdj.category = cc("https://bluebutton.cms.gov/fhir/CodeSystem/Adjudication", "CLM_LINE_VCCN_ADMIN_FEE_AMT") "Set category";
        vccnFeeAmt -> tgtAdj.amount = create('Money') as tgtMoney then addMoney(vccnFeeAmt, tgtMoney) "Add vaccination administration fee amount";
    } "Add Vaccination Administration Fee";

    src.CLM_LINE_NCVRD_PD_AMT as ncvrdPdAmt -> tgt.adjudication = create('BackboneElement') as tgtAdj then {
        src -> tgtAdj.category = cc("https://bluebutton.cms.gov/fhir/CodeSystem/Adjudication", "CLM_LINE_NCVRD_PD_AMT") "Set category";
        ncvrdPdAmt -> tgtAdj.amount = create('Money') as tgtMoney then addMoney(ncvrdPdAmt, tgtMoney) "Add noncovered paid amount";
    } "Add Noncovered Paid Amount";

    src.CLM_LINE_OTHR_TP_PD_AMT as othrTpPdAmt -> tgt.adjudication = create('BackboneElement') as tgtAdj then {
        src -> tgtAdj.category = cc("https://bluebutton.cms.gov/fhir/CodeSystem/Adjudication", "CLM_LINE_OTHR_TP_PD_AMT") "Set category";
        othrTpPdAmt -> tgtAdj.amount = create('Money') as tgtMoney then addMoney(othrTpPdAmt, tgtMoney) "Add other third party paid amount";
    } "Add Other Third Party Paid Amount";

    src.CLM_CMS_CALCD_MFTR_DSCNT_AMT as mftrDscntAmt -> tgt.adjudication = create('BackboneElement') as tgtAdj then {
        src -> tgtAdj.category = cc("https://bluebutton.cms.gov/fhir/CodeSystem/Adjudication", "CLM_CMS_CALCD_MFTR_DSCNT_AMT") "Set category";
        mftrDscntAmt -> tgtAdj.amount = create('Money') as tgtMoney then addMoney(mftrDscntAmt, tgtMoney) "Add manufacturer discount amount";
    } "Add CMS calculated manufacturer discount amount";

    src.CLM_LINE_REBT_PASSTHRU_POS_AMT as rebtPassthruPosAmt -> tgt.adjudication = create('BackboneElement') as tgtAdj then {
        src -> tgtAdj.category = cc("https://bluebutton.cms.gov/fhir/CodeSystem/Adjudication", "CLM_LINE_REBT_PASSTHRU_POS_AMT") "Set category";
        rebtPassthruPosAmt -> tgtAdj.amount = create('Money') as tgtMoney then addMoney(rebtPassthruPosAmt, tgtMoney) "Add reimbursement pass through positive amount";
    } "Add Reimbursement Pass Through Positive Amount";

    src.CLM_PHRMCY_PRICE_DSCNT_AT_POS_AMT as phrmcyPriceDscntAtPosAmt -> tgt.adjudication = create('BackboneElement') as tgtAdj then {
        src -> tgtAdj.category = cc("https://bluebutton.cms.gov/fhir/CodeSystem/Adjudication", "CLM_PHRMCY_PRICE_DSCNT_AT_POS_AMT") "Set category";
        phrmcyPriceDscntAtPosAmt -> tgtAdj.amount = create('Money') as tgtMoney then addMoney(phrmcyPriceDscntAtPosAmt, tgtMoney) "Add pharmacy price discount at POS amount";
    } "Add Pharmacy Price Discount At POS Amount";
}

//To get this info, join CLM with V2_MDCR_CNTRCT_PBP_NUM on the claim's CLM.CLM_SBMTR_CNTRCT_NUM + CLM.CLM_SBMTR_CNTRCT_PBP_NUM. 
group createInsurerOrgPartD(source src: ExplanationOfBenefitPharmacy, target tgtOrg: Organization){
  src -> tgtOrg.id = "insurer-org" "Set org id";
  src -> tgtOrg.meta = create('Meta') as tgtMeta then {
    src -> tgtMeta.profile = "http://hl7.org/fhir/us/carin-bb/StructureDefinition/C4BB-Organization|2.1.0" "Set org profile";
    src -> tgtMeta.profile = "http://hl7.org/fhir/us/core/StructureDefinition/us-core-organization|6.1.0" "Set org profile";
  } "Set organization metadata";
  src -> tgtOrg.active = "true" "set org status";
  src.CNTRCT_PBP_NAME as pbpName -> tgtOrg.name = pbpName "set org name";
}
