// These are the basic imports that the Jenkins interactive script console
// automatically includes.
import jenkins.*;
import jenkins.model.*;
import hudson.*;
import hudson.model.*;

// Enable the slave agent port, because most everyone will want it.
// This will use a setter, which will set the value (to a random port)
// and also initialize the slave agent.
if (Jenkins.instance.slaveAgentPort != 0) {
  Jenkins.instance.slaveAgentPort=0
  println "Changed: slave agent configuration."
}

// Set the Jenkins external URL, if defined.
// (Hat tip: http://stackoverflow.com/questions/30355079/jenkins-setting-root-url-via-groovy-api.)
def externalUrl = "{{ jenkins_url_external | default('') | trim }}" ?: null
def locationConfig = JenkinsLocationConfiguration.get()
println("Configuring Jenkins External URL (current value: '${locationConfig.url}', target value: '${externalUrl}')...")
if (externalUrl != locationConfig.url) {
  locationConfig.url = externalUrl
  locationConfig.save()
  println "Changed: external URL."
}

// Set the Jenkins proxy config, if defined.
def proxyName = "{{ jenkins_http_proxy_server | default('') | trim }}"
def proxyPortText = "{{ jenkins_http_proxy_port | default('') | trim }}"
def proxyNoProxyHosts = "{{ jenkins_http_proxy_no_proxy_hosts | default([]) | join('\n') }}"
if (proxyName == "") {
  if (Jenkins.instance.proxy != null) {
    Jenkins.instance.proxy = null
    ProxyConfiguration.getXmlFile().delete()
    println "Changed: removed proxy configuration"
  }
} else if (
    Jenkins.instance.proxy == null
    || Jenkins.instance.proxy.name != proxyName
    || "" + Jenkins.instance.proxy.port != proxyPortText
    || Jenkins.instance.proxy.noProxyHost != proxyNoProxyHosts
) {
  try { proxyPort = Integer.parseInt(proxyPortText) } catch(NumberFormatException e) { throw new IllegalArgumentException("Invalid proxy port: ${proxyPortText}", e) }
  proxyDesired = new ProxyConfiguration(proxyName, proxyPort, null, null, proxyNoProxyHosts)
  proxyDesired.save()
  Jenkins.instance.proxy = ProxyConfiguration.load()
  println "Changed: proxy configuration."
}
